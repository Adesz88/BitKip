import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.26.0'
}

group 'ir.darkdeveloper'
version '0.3.0'
sourceCompatibility = '19'
targetCompatibility = '19'
mainClassName = 'ir.darkdeveloper.bitkip.BitKip'

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainClass = mainClassName
    mainModule = 'ir.darkdeveloper.bitkip'
}

javafx {
    version = '19.0.2.1'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.swing']
}

dependencies {
    implementation 'org.controlsfx:controlsfx:11.1.0'
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.xerial:sqlite-jdbc:3.40.0.0'
    implementation 'org.jsoup:jsoup:1.15.3'
    implementation 'org.eclipse.jetty:jetty-servlet:11.0.15'
    implementation 'org.eclipse.jetty:jetty-server:11.0.15'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.0'
    implementation 'ch.qos.logback:logback-classic:1.4.7'
    implementation 'org.kordamp.ikonli:ikonli-fontawesome5-pack:12.3.1'
    implementation 'org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0'
    implementation 'io.projectreactor:reactor-core:3.4.24'
    implementation 'net.java.dev.jna:jna:5.13.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    testCompileOnly 'org.projectlombok:lombok:1.18.26'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'
}

test {
    useJUnitPlatform()
}

jlink {
    launcher {
        name = 'BitKip'
    }
    jpackage {
        if (OperatingSystem.current().windows) {
            appVersion = version
            installerName = 'BitKip'
            installerOptions = ['--win-menu', '--win-shortcut',
                                '--icon', 'src/main/resources/ir/darkdeveloper/bitkip/icons/logo.ico',
                                '--vendor', 'DarkDeveloper']
            imageOptions = ['--icon', 'src/main/resources/ir/darkdeveloper/bitkip/icons/logo.ico']
        }
    }
    mergedModule {
        excludeProvides servicePattern: 'reactor.blockhound.*'
    }
}

jlinkZip {
    group = 'distribution'
}